#version 450

layout (constant_id = 0) const uint MAX_LIST_SIZE = 20;
layout (constant_id = 1) const uint lightCount = 1;
layout (constant_id = 2) const uint CLUSTER_X = 0;
layout (constant_id = 3) const uint CLUSTER_Y = 0;
layout (constant_id = 4) const uint CLUSTER_Z = 0;

layout(binding = 0, r16ui) uniform writeonly uimage3D clusterImage;

struct ClusterData{
	uint lights[MAX_LIST_SIZE];
};
layout(binding = 1) writeonly buffer ClusterDatas{
	ClusterData clusterDatas[];
};

struct Light{
	vec4 sphere;
	vec3 color;
};
layout(binding = 2) readonly buffer GlobalLights{
	Light lights[];
}globalLights;

struct ClusterFrustum{
	vec4 planes[6];
};
layout(binding = 3) readonly buffer ClusterFrustums{
	ClusterFrustum frustums[];
};

layout(local_size_x = 16) in;

// 平面法线均指向frustum内部
bool frustumSphere(ClusterFrustum frustum, vec4 pos, float radius){
	// Check sphere against frustum planes
	for (int i = 0; i < 6; i++){
		if (dot(pos, frustum.planes[i]) + radius < 0.0){
			return false;
		}
	}
	return true;
}

void main(){
	uint clusterIndex = gl_GlobalInvocationID.x;
	uvec3 clusterCoord = uvec3(
		clusterIndex % (CLUSTER_X * CLUSTER_Y) % CLUSTER_X,
		clusterIndex % (CLUSTER_X * CLUSTER_Y) / CLUSTER_X,
		clusterIndex / (CLUSTER_X * CLUSTER_Y)
	);

	uint offset = 0;
	uint size = 0;
	ClusterFrustum frustum = frustums[clusterIndex];
	for(uint i = 0; i < lightCount; ++i){
		Light light = globalLights.lights[i];

		if(frustumSphere(frustum, vec4(light.sphere.xyz, 1.0), light.sphere.w))
		{
			if(offset >= MAX_LIST_SIZE)
				continue;

			clusterDatas[clusterIndex].lights[offset] = i;
			++offset;
			++size;
		}
	}

	imageStore(clusterImage, ivec3(clusterCoord), uvec4(size));
}